plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.31'
    id 'org.unbroken-dome.test-sets' version '2.1.1'
    id 'com.diffplug.gradle.spotless' version '3.23.0'
    id 'com.adarshr.test-logger' version '1.6.0'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

ext {
    loadPropsFromFile = { String path ->
        Properties props = new Properties()
        File propsFile = file(path)
        if (propsFile.exists()) {
            propsFile.withReader { reader ->
                props.load(reader)
            }
        }
        props
    }
    versions = loadPropsFromFile("$rootDir/versions.properties")
}

repositories {
    mavenCentral()
}

testSets { integrationTest { dirName = 'integration-test' } }
integrationTest.mustRunAfter test

test {
    useJUnitPlatform()
    systemProperties = [
            'junit.jupiter.extensions.autodetection.enabled': 'true',
            'junit.jupiter.testinstance.lifecycle.default'  : 'per_class',
            'java.util.logging.manager'                     : 'org.apache.logging.log4j.jul.LogManager'
    ]
}

testlogger { showStandardStreams true }

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-noarg'
    implementation 'org.jetbrains.kotlin:kotlin-native-utils'

    implementation "io.github.microutils:kotlin-logging:${versions.kLogging}"
    implementation("com.github.shyiko:ktlint:${versions.ktlint}") {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.slf4j'
    }

    implementation "org.web3j:core:${versions.web3j}"
    testImplementation(group: 'org.web3j', name: 'core', version: versions.web3j, classifier: 'tests')

    runtimeOnly "ch.qos.logback:logback-core:${versions.logback}"
    runtimeOnly "ch.qos.logback:logback-classic:${versions.logback}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit5}"
    testImplementation "org.assertj:assertj-core:${versions.assertj}"
    testImplementation "junit:junit:${versions.junit4}"

    testImplementation("io.mockk:mockk:${versions.mockk}") {
        exclude group: 'org.jetbrains.kotlin'
    }

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit5}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${versions.junit5}"

    integrationTestImplementation "org.testcontainers:testcontainers:${versions.testcontainers}"
    integrationTestImplementation "org.testcontainers:junit-jupiter:${versions.testcontainers}"
}

spotless {
    kotlin {
        // This path needs to be relative to each project
        target fileTree('.') {
            include '**/*.kt'
            exclude '**/.gradle/**'
        }
        ktlint(versions.ktlint)
        trimTrailingWhitespace()
        endWithNewline()
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

compileIntegrationTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}